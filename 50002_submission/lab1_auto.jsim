.include "nominal.jsim"
* .include "lab1checkoff.jsim"

* inverter: input is a, output is z
.subckt inv a z
MPD1 z a 0 0 NENH sw=16 sl=1
MPU1 z a vdd vdd PENH sw=16 sl=1
.ends

* 2-input nor: inputs are a and b, output is z
.subckt nor2 a b z
MPD1 z a 0 0 NENH sw=8 sl=1
MPD2 z b 0 0 NENH sw=8 sl=1
MPU1 z a 1 vdd PENH sw=9 sl=1
MPU2 1 b vdd vdd PENH sw=9 sl=1
.ends

* 2-input nand: inputs are a and b, output is z
.subckt nand2 a b z
MPD1 z a 1 0 NENH sw=8 sl=1
MPD2 1 b 0 0 NENH sw=8 sl=1
MPU1 z a vdd vdd PENH sw=9 sl=1
MPU2 z b vdd vdd PENH sw=9 sl=1
.ends

* 2-input and: inputs are a and b, output is z
.subckt and2 a b z
X1 a b 1 nand2
X2 1 z inv
.ends

* 2-input or: inputs are a and b, output is z
.subckt or2 a b z
X1 a b 1 nor2
X2 1 z inv
.ends

.subckt xor2 a b z
MPP11 1 a vdd vdd PENH sw=4 sl=1
MPP12 z 3 1 vdd PENH sw=4 sl=1
MPN13 z a 4 0 NENH sw=2 sl=1
MPN14 4 b 0 0 NENH sw=2 sl=1
MPP15 1 b vdd vdd PENH sw=4 sl=1
MPN16 z 3 0 0 NENH sw=2 sl=1
X1 a b 3 nor2
*END ANSWER
.ends

.subckt xnor2 a b z
MPP11 z 1 vdd vdd PENH sw=4 sl=1
MPN12 z 1 2 0 NENH sw=2 sl=1
MPN13 2 b 0 0 NENH sw=2 sl=1
MPP14 3 b vdd vdd PENH sw=4 sl=1
MPP15 z a 3 vdd PENH sw=4 sl=1
MPN16 2 a 0 0 NENH sw=2 sl=1
X1 a b 1 nand2
.ends

Wtest a1 b1 nrz(0v,5v,10ns,0ns,0.01ns,0.01ns) 0 1 2 3 0
.tran 100ns
Xtest1 a1 b1 z1 and2
Xtest2 a1 b1 z2 or2
Xtest3 a1 b1 z3 xor2
Xtest4 a1 b1 z4 xnor2

.plot a1
.plot b1
.plot z1
.plot z2
.plot z3
.plot z4